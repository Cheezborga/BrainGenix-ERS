// Copyright 2020, Collabora, Ltd.
// SPDX-License-Identifier: BSL-1.0

buildscript {
    ext {
        kotlinVersion = '1.4.10'
        latestAboutLibsRelease = "8.5.0"

        androidxCoreVersion = "1.3.2"
        androidxAnnotationVersion = "1.1.0"
        androidxAppCompatVersion = "1.2.0"
        androidxLifecycleVersion = "2.2.0"
        androidxConstraintLayoutVersion = '2.0.4'

        hiltVersion = "2.29.1-alpha"

        // Saw some breakage when updating to 1.2?
        materialVersion = "1.1.0"
    }
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.mikepenz.aboutlibraries.plugin:aboutlibraries-plugin:${latestAboutLibsRelease}"
        classpath 'com.quittle:svg-2-android-vector:0.0.5'
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hiltVersion"

    }
}
plugins {
    // Used for getting the eigen dir, explicit python binary, etc. from local.properties
    id 'com.github.b3er.local.properties' version '1.1'

    // For getting git describe data and formatting it how Android wants.
    id "com.gladed.androidgitversion" version "0.4.13"

    // For downloading e.g. the CDDL (for transitive dependencies of hilt)
    id "de.undercouch.download" version "4.1.1"
}

ext {
    ndk_version = '21.3.6528147'
    sharedTargetSdk = 30
    sharedMinSdk = 26

    // If you are building on Windows, you will need to explicitly set eigenIncludeDir in your
    // local.properties file since the default value provided below only makes sense on *nix
    eigenIncludeDir = project.findProperty('eigenIncludeDir') ?: '/usr/include/eigen3'

    // If you're having trouble with a "can't find python" CMake error, you can specify the path to
    // Python 3 explicitly in local.properties with a property named "pythonBinary"
    pythonBinary = project.findProperty('pythonBinary')
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}
